/*

    Minimum Profit 5.x
    A Programmer's Text Editor

    Version Control System support

    Copyright (C) 1991-2019 Angel Ortega <angel@triptico.com>

    This program is free software; you can redistribute it and/or
    modify it under the terms of the GNU General Public License
    as published by the Free Software Foundation; either version 2
    of the License, or (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program; if not, write to the Free Software
    Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA  02111-1307, USA.

    http://triptico.com

*/

/** mp_doc commit subclass **/

global mp_doc_vcs = new(mp_doc, { name: '.COMMIT_MESSAGE' });


/** data **/

mp.vcs = {
    git: {
        id:     'Git',
        check:  [ '.git', '../.git', '../../.git', '../../../.git' ],
        opts:   {
            "git add '%s'"      => NULL,
            'git log'           => { unique_log: 1 },
            "git log '%s'"      => { unique_log: 1 },
            'git log -p'        => { unique_log: 1, diff: 1 },
            "git log -p '%s'"   => { unique_log: 1, diff: 1 },
            'git diff'          => { unique_log: 1, diff: 1 },
            "git diff '%s'"     => { unique_log: 1, diff: 1 },
            'git status'        => NULL,
            "git blame '%s'"    => { unique_log: 1 },
            "git stash"         => NULL,
            "git stash apply"   => NULL,
            "git push"          => NULL,
            "git pull"          => NULL,
            "git commit -a"     => { commit_cmd: "git commit -a -F '%s'" }
        }
    },
    svn: {
        id:     'SVN',
        check:  [ '.svn', '../.svn', '../../.svn', '../../../.svn' ],
        opts:   {
            'svn diff'            => { unique_log: 1, diff: 1 },
            'svn status'          => NULL,
            'svn log'             => { unique_log: 1 },
            'svn log --diff'      => { unique_log: 1, diff: 1 },
            "svn diff '%s'"       => { unique_log: 1, diff: 1 },
            "svn log '%s'"        => { unique_log: 1 },
            "svn log '%s' --diff" => { unique_log: 1, diff: 1 },
            "svn add '%s'"        => NULL,
            "svn annotate '%s'"   => { unique_log: 1 },
            "svn update"          => NULL,
            "svn revert '%s'"     => NULL,
            "svn commit"          => { commit_cmd: "svn commit -F '%s'" }
        }
    },
    mercurial: {
        id:     'Mercurial',
        check:  [ '.hg', '../.hg', '../../.hg' ],
        opts:   {
            'hg diff'          => { unique_log: 1, diff: 1 },
            'hg status'        => NULL,
            'hg log'           => { unique_log: 1 },
            "hg diff '%s'"     => { unique_log: 1, diff: 1 },
            "hg log '%s'"      => { unique_log: 1 },
            "hg add '%s'"      => NULL,
            "hg push"          => NULL,
            "hg pull"          => NULL,
            "hg annotate '%s'" => { unique_log: 1 },
            "hg commit -A"     => { commit_cmd: "hg commit -A -l '%s'" }
        }
    }
};


sub mp.vcs_form()
{
    local vcs = NULL;

    /* find which VCS apply to current directory */
    foreach (n, mp.vcs) {
        foreach (dir, n.check) {
            if (stat(dir) != NULL) {
                vcs = n;
                break;
            }
        }

        if (vcs != NULL)
            break;
    }

    if (vcs == NULL)
        mp.alert(L("This directory is not under a supported version control system"));
    else {
        local opts = keys(vcs.opts)->sort();
        local cmd_list = opts->map(sub (e) {
                            sprintf(e, regex(mp.active.name, '/[^\/\\]+$/'));
                         });

        local form = mp.form(
            [
                {
                    label:  sprintf(L("Available %s commands"), vcs.id) ~ ':',
                    type:   'list',
                    list:   cmd_list,
                    value:  mp.vcs_target
                }
            ]
        );

        if (form != NULL) {
            mp.vcs_target = form[0];

            local opt = vcs.opts[opts[mp.vcs_target]];
            local cmd = cmd_list[mp.vcs_target];

            if (opt.commit_cmd) {
                /* create a commit message editor document */
                local msg = new(mp_doc_vcs);

                /* don't store these files in the mp.state */
                msg.no_state = 1;

                msg.vcs = {
                    commit_cmd: opt.commit_cmd,
                    log_name:   sprintf(L("<%s output>"), vcs.id)
                };

                mp.add(msg);
            }
            else {
                local log;

                /* unique log for this command? */
                if (opt.unique_log) {
                    log           = mp.open(sprintf(L("<%s output>"), cmd));
                    log.txt.lines = [ '' ];
                    log.txt.y     = 0;
                }
                else {
                    /* common log */
                    log = mp.open(sprintf(L("<%s output>"), vcs.id));

                    log->move_eof();
                    log->insert(cmd ~ ":\n\n");
                }

                log->actions.exec_command(cmd);

                if (opt.unique_log) {
                    if (opt.diff)
                        log.syntax = mp.get_syntax("diff");

                    log->move_bof();
                }
                else
                    log->insert_newline();

                log.txt.mod     = 0;
                log.undo_q      = [];
                log.read_only   = 1;
            }
        }
    }
};

/** code **/

sub mp_doc_vcs.query_close(doc)
/* mp_doc.query_close overload for commit messages */
{
    local r;

    /* call superclass query close */
    if ((r = mp_doc.query_close(doc)) == 1) {
        /* has the commit message been saved? */
        if (stat(doc.name) != NULL) {
            local cmd = sprintf(doc.vcs.commit_cmd, doc.name);
            local log = mp.open(doc.vcs.log_name);

            log->move_eof();
            log->insert(cmd ~ ":\n\n");
            log->actions.exec_command(cmd);
            log->insert_newline();

            log.txt.mod     = 0;
            log.undo_q      = [];
            log.read_only   = 1;

            /* bring doc to active back */
            mp.open(doc.name);
        }

        unlink(doc.name);
    }

    return r;
}
